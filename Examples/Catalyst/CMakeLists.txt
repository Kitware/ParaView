cmake_minimum_required(VERSION 3.3)
project(CatalystExamples CXX C)

set(USE_CATALYST ON CACHE BOOL "Link the simulator with Catalyst")
if(USE_CATALYST)
  enable_language(CXX)
  find_package(ParaView 4.2 REQUIRED COMPONENTS vtkPVPythonCatalyst)
  include("${PARAVIEW_USE_FILE}")
  add_definitions("-DUSE_CATALYST")
  if(NOT PARAVIEW_USE_MPI)
    message(SEND_ERROR "ParaView must be built with MPI enabled")
  endif()
else()
  find_package(MPI REQUIRED)
  include_directories(${MPI_C_INCLUDE_PATH})
endif()

option(BUILD_TESTING "Build Testing" OFF)
# Setup testing.
if (BUILD_TESTING)
  include(CTest)
endif()

add_subdirectory(CxxOverlappingAMRExample)
add_subdirectory(CxxNonOverlappingAMRExample)
add_subdirectory(CFullExample2)
add_subdirectory(CxxFullExample)
add_subdirectory(CFullExample)
add_subdirectory(CxxVTKPipelineExample)
add_subdirectory(CxxImageDataExample)
add_subdirectory(CxxMultiPieceExample)
add_subdirectory(CxxPVSMPipelineExample)
add_subdirectory(CxxMappedDataArrayExample)
add_subdirectory(MPISubCommunicatorExample)
add_subdirectory(PythonDolfinExample)
add_subdirectory(CxxParticlePathExample)
add_subdirectory(CxxSOADataArrayExample)

set(BUILD_FORTRAN_EXAMPLES OFF CACHE BOOL "Build Fortran Catalyst Examples")
if(BUILD_FORTRAN_EXAMPLES)
  # Theoretically, CheckFortran should not be needed, but
  # enable_language(OPTIONAL) fails with Ninja generator.
  include(CheckFortran)
  if(CMAKE_Fortran_COMPILER)
    enable_language(Fortran OPTIONAL)
  endif()
  if(CMAKE_Fortran_COMPILER_WORKS)
    add_subdirectory(Fortran90FullExample)
    add_subdirectory(FortranPoissonSolver)
  endif()
endif()
